/********************************************************
* Kuvaus: Lääkekorvauksien lainsäädäntöä makroina       *
* Viimeksi päivitetty: 11.9.2019                        *
********************************************************/

/* SOTE-SISUA varten tehdyt SAS-makrot                     */
/* Käännetty R:n laakesimufunk-paketista SAS:iin           */
/* Pekka Heino 11.9.2019                                   */

/* Tiedosto sisältää seuraavat makrot:

1. LaakAlkuOmaVastuu = Lääkkeiden alkuomavastuun laskukaava
2. LaakKorvausEuro = Lääkkeiden korvausten laskukaava (ei kattokorvausta), euromääräinen alkuomavastuu
3. LaakResOmaVastuuEuro = Lääkkeiden reseptin omavastuun laskukaava, euromääräinen alkuomavastuu
4. LaakKorvausPros = Lääkkeiden korvausten laskukaava (ei kattokorvausta), prosenttiperusteinen alkuomavastuu
5. LaakResOmaVastuuPros = Lääkkeiden reseptin omavastuun laskukaava, prosenttiperusteinen alkuomavastuu
6. LaakKattoKorvaus = Lääkkeiden kattokorvausten laskukaava
7. LaakTaksa = Lääkkeiden taksojen laskukaava (EI VALMIS) */

/* Koodissa näitä yleensä käytetään järjestyksessä 7, 1, (2, 3) tai (4, 5), 6 . Tosin ensimmäisiä ei ole pakko
suorittaa, jos niihin ei tee muutoksia. */

/* 1. Makro, joka laskee alkuomavastuun määrän kyseisen oston kohdalla */
/* Jotta makro toimisi oikein, pitää datan olla järjestettynä valmiiksi seuraavien muuttujien mukaan:
ID, OTPVM, LAJI_NUM, RVMK */
*Makron parametrit:
	tulos: Makron tulosmuuttuja, alkuomavastuun määrä
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään
	mkuuk: Kuukausi, jonka lainsäädäntöä käytetään
	ikaraj: Ikä
	rvmk: Rvmk
	rvmk_k: Rvmk-kertymä
	;

%MACRO LaakAlkuOmaVastuu(tulos, mvuosi, mkuuk, minf, ikaraj, rvmk, rvmk_k)/
DES = 'LAAKE: Alkuomavastuun määrä lääkkeen kohdalla';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	*%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);

	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
* Lasketaan ketkä ylittävät alkuomavastuun;
	RETAIN OVAL_CUMU;
	IF &rvmk_k >= &AlkuomavTaso1 
		THEN OVAL=1;
		ELSE OVAL=0;
	IF &ikaraj < &AlkuomavIkaraja1 
		THEN OVAL=0;
* Lasketaan, kuinka monta ostosta on yli alkuomavastuun (OVAL_CUMU=1 on rajaosto);
/* Jos data on järjestetty ennen makron suorittamista, ei järjestämistä tarvitse tehdä */
	IF first.hnro THEN DO;
		OVAL_CUMU=0;
	  END;
	OVAL_CUMU = OVAL_CUMU + OVAL;
	* Lasketaan alkuomavastuun määrä;
	IF OVAL=0 THEN &tulos = &rvmk;
		ELSE &tulos = 0;
	IF OVAL_CUMU=1 THEN &tulos = &AlkuomavTaso1 - (&rvmk_k - &rvmk);
	/* Ikäraja, minkä alle ei ole omavastuuta. Vuoden lopun iän mukaan, kuten laissakin on */
	IF &ikaraj <= &AlkuomavIkaraja1 THEN &tulos = 0;
	* Jos OVAL_CUMU > 1, ei alkuomavastuuta ;
	*&tulos = ALKUOMAV_SIMUL;
%MEND LaakAlkuOmaVastuu;

/* 2. Makro, joka laskee lääkkeen korvauksen määrän (ilman kattokorvausta) kyseisen oston kohdalla */

*Makron parametrit:
	tulos: Makron tulosmuuttuja, korvauksen määrä
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään
	mkuuk: Kuukausi, jonka lainsäädäntöä käytetään
	klaji: Lääkelaji;

%MACRO LaakKorvausEuro(tulos, mvuosi, mkuuk, minf, klaji)/
DES = 'LAAKE: Korvauksen määrä (ilman kattokorvausta) lääkkeen kohdalla, euromääräinen omavastuu';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);
	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
	/* Kokonaan alle alkuomavastuun olevissa lääkkeissä korvaus = 0 */
	IF OVAL_CUMU = 0 THEN &tulos = 0;
	/* Rajaoston kohdalla osa on korvattavaa ja osa alkuomavastuun piirissä */
	ELSE IF OVAL_CUMU = 1 AND ANJA = "" AND ika > &AlkuomavIkaraja1 THEN DO;
		IF RVMK_KERTY_SIMU - &AlkuomavTaso1 >= &YlempiEritKorvKiintea THEN DO;
			&tulos = RVMK_KERTY_SIMU - &AlkuomavTaso1 - &YlempiEritKorvKiintea;
		END;
		ELSE IF RVMK_KERTY_SIMU - &AlkuomavTaso1 < &YlempiEritKorvKiintea THEN DO;
			&tulos = 0;
		END;
	END;

	/* 18-vuotiaaksi saakka kaikki ovat korvattavia */
	ELSE IF (OVAL_CUMU > 1 OR ika <= &AlkuomavIkaraja1) AND ANJA = "" THEN DO;
		IF rvmk_SIMU < &YlempiEritKorvKiintea THEN &tulos = 0;
		ELSE IF rvmk_SIMU >= &YlempiEritKorvKiintea THEN &tulos = KUST_SIMUL - &YlempiEritKorvKiintea;
	END;

	/* Annosjakelut lasketaan eri tavalla */
	ELSE IF OVAL_CUMU = 1 AND ANJA ne "" AND ika > &AlkuomavIkaraja1 THEN DO;
		IF RVMK_KERTY_SIMU - &AlkuomavTaso1 >= 2*&YlempiEritKorvKiinteaPoik THEN 
				&tulos = (RVMK_KERTY_SIMU - &AlkuomavTaso1) - 2*&YlempiEritKorvKiinteaPoik;
		ELSE IF RVMK_KERTY_SIMU - &AlkuomavTaso1 < 2*&YlempiEritKorvKiinteaPoik THEN 
					&tulos = 0;
	END;

	/* 18-vuotiaaksi saakka kaikki ovat korvattavia */
	ELSE IF (OVAL_CUMU > 1 OR ika <= &AlkuomavIkaraja1) AND ANJA NE "" THEN DO;
		IF rvmk_SIMU < 2*&YlempiEritKorvKiinteaPoik THEN 
				&tulos = 0;
		ELSE IF rvmk_SIMU >= 2*&YlempiEritKorvKiinteaPoik THEN 
					&tulos = rvmk_SIMU - 2*&YlempiEritKorvKiinteaPoik;
	END;
		

%MEND LaakKorvausEuro;

/* 3. Makro, joka laskee lääkkeen reseptin omavastuun määrän kyseisen oston kohdalla */
/* Periaatteessa reseptin omavastuu on vastakohta korvaukselle */
*Makron parametrit:
	tulos: Makron tulosmuuttuja, reseptin omavastuun määrä
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään
	klaji: Lääkelaji;

%MACRO LaakResOmaVastuuEuro(tulos, mvuosi, mkuuk, minf, klaji)/
DES = 'LAAKE: Korvauksen määrä (ilman kattokorvausta) lääkkeen kohdalla, euromääräinen omavastuu';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);

	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
	/* Kokonaan alle alkuomavastuun olevissa lääkkeissä omavastuu = 0 (lisätään alkuomavastuu myöhemmin) */
	IF OVAL_CUMU = 0 THEN &tulos = 0;
	/* Rajaoston kohdalla osa on korvattavaa ja osa alkuomavastuun piirissä */
	IF OVAL_CUMU = 1 AND ANJA = "" and ika > &AlkuomavIkaraja1 THEN DO;
		IF RVMK_KERTY_SIMU - &AlkuomavTaso1 >= &YlempiEritKorvKiintea THEN DO;
				&tulos = &YlempiEritKorvKiintea;
			END;
			ELSE IF RVMK_KERTY_SIMU - &AlkuomavTaso1 < &YlempiEritKorvKiintea THEN DO;
					&tulos = RVMK_KERTY_SIMU - &AlkuomavTaso1;
				END;
		END;
	/* 18-vuotiaaksi saakka kaikki ovat korvattavia */
	IF (OVAL_CUMU > 1 OR ika <= &AlkuomavIkaraja1) AND ANJA = "" THEN DO;
		IF rvmk_SIMU < &YlempiEritKorvKiintea THEN DO;
				&tulos = rvmk_SIMU;
			END;
			ELSE IF rvmk_SIMU >= &YlempiEritKorvKiintea THEN DO;
					&tulos = &YlempiEritKorvKiintea;
				END;
		END;
	/* Annosjakelut lasketaan eri tavalla */
	IF OVAL_CUMU = 1 AND ANJA NE "" AND ika > &AlkuomavIkaraja1 THEN DO;
		IF RVMK_KERTY_SIMU - &AlkuomavTaso1 >= 2*&YlempiEritKorvKiinteaPoik THEN DO;
				&tulos = 2*&YlempiEritKorvKiinteaPoik;
			END;
			ELSE IF RVMK_KERTY_SIMU - &AlkuomavTaso1 < 2*&YlempiEritKorvKiinteaPoik THEN DO;
					&tulos = RVMK_KERTY_SIMU - &AlkuomavTaso1;
				END;
		END;

	/* 18-vuotiaaksi saakka kaikki ovat korvattavia */
	IF (OVAL_CUMU > 1 OR ika <= &AlkuomavIkaraja1) AND ANJA NE "" THEN DO;
		IF rvmk_simu < 2*&YlempiEritKorvKiinteaPoik THEN DO;
				&tulos = rvmk_SIMU;
			END;
			ELSE IF rvmk_SIMU >= 2*&YlempiEritKorvKiinteaPoik THEN DO;
					&tulos = 2*&YlempiEritKorvKiinteaPoik;
				END;
		END;
%MEND LaakResOmavastuuEuro;

/* 4. Makro, joka laskee lääkkeen korvauksen määrän (ilman kattokorvausta) kyseisen oston kohdalla */

*Makron parametrit:
	tulos: Makron tulosmuuttuja, korvauksen määrä
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään
	mkuuk: Kuukausi, jonka lainsäädäntöä käytetään
	klaji: Lääkelaji;

%MACRO LaakKorvausPros(tulos, mvuosi, mkuuk, minf, klaji)/
DES = 'LAAKE: Korvauksen määrä (ilman kattokorvausta) lääkkeen kohdalla, prosenttiperusteinen omavastuu';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);

	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
	/* Kokonaan alle alkuomavastuun olevissa lääkkeissä korvaus = 0 */
	IF OVAL_CUMU = 0 THEN &tulos = 0;
	/* Rajaoston kohdalla osa on korvattavaa ja osa alkuomavastuun piirissä */
	IF (OVAL_CUMU = 1 AND ika > &AlkuomavIkaraja1) THEN DO;
		IF laji IN ("O","U") THEN DO;
				&tulos = ROUND(&PerusKorvPros*(RVMK_KERTY_SIMU - &AlkuomavTaso1), 0.01); 
			END;
		IF laji in ("Y") THEN DO;
				&tulos = ROUND(&AlempiEritKorvPros*(RVMK_KERTY_SIMU - &AlkuomavTaso1), 0.01); 
			END;
		END;
	/* 18-vuoteen saakka kaikki ovat korvattavia */
	IF (OVAL_CUMU > 1 OR ika <= &AlkuomavIkaraja1) THEN DO;
		IF laji in ("O","U") THEN DO;
				&tulos = ROUND(&PerusKorvPros*RVMK_SIMU, 0.01); 
			end;
		IF laji IN ("Y") THEN DO;
				&tulos = round(&AlempiEritKorvPros*RVMK_SIMU, 0.01); 
			END;
		END;
	
	
%MEND LaakKorvausPros;

/* 5. Makro, joka laskee lääkkeen reseptin omavastuun määrän kyseisen oston kohdalla */
/* Periaatteessa reseptin omavastuu on vastakohta korvaukselle */
*Makron parametrit:
	tulos: Makron tulosmuuttuja, reseptin omavastuun määrä
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään
	mkuuk: Kuukausi, jonka lainsäädäntöä käytetään
	klaji: Lääkelaji;
%MACRO LaakResOmaVastuuPros(tulos, mvuosi, mkuuk, minf, klaji)/
DES = 'LAAKE: Korvauksen määrä (ilman kattokorvausta) lääkkeen kohdalla, prosenttiperusteinen omavastuu';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);

	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
	/* Kokonaan alle alkuomavastuun olevissa lääkkeissä omavastuu = 0 (lisätään alkuomavastuu myöhemmin) */
	IF OVAL_CUMU = 0 THEN &tulos = 0;
	/* Rajaoston kohdalla osa on korvattavaa ja osa alkuomavastuun piirissä */
	IF OVAL_CUMU=1 AND ika > &AlkuomavIkaraja1 THEN DO;
			IF laji IN ("O","U") THEN DO;
				&tulos = ROUND((1-&PerusKorvPros)*(RVMK_KERTY_SIMU - &AlkuomavTaso1), 0.01); 
			end;
		IF laji IN ("Y") THEN DO;
				&tulos = ROUND((1-&AlempiEritKorvPros)*(RVMK_KERTY_SIMU - &AlkuomavTaso1), 0.01); 
			end;	
		end;

	/* Alle 18-vuotiailla kaikki ovat korvattavia. Tai on ollut aina tähän asti. */
	IF (OVAL_CUMU > 1 OR ika <= &AlkuomavIkaraja1) THEN DO;
		IF laji IN ("O","U") THEN DO;
				&tulos = ROUND((1-&PerusKorvPros)*RVMK_SIMU, 0.01); 
			END;
		IF laji IN ("Y") THEN DO;
				&tulos = ROUND((1-&AlempiEritKorvPros)*RVMK_SIMU, 0.01); 
			END;
		END;
	
%MEND LaakResOmaVastuuPros;
/* 6. Makro, joka laskee lääkkeen kattokorvauksen määrän kyseisen oston kohdalla */
*Makron parametrin:
	tulos: Makron tulosmuuttuja, kattokorvauksen määrä
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään
	mkuuk: Kuukausi, jonka lainsäädäntöä käytetään
	;

%MACRO LaakKattoKorvausMaksettava(tulos, mvuosi, mkuuk, minf)/
DES = 'LAAKE: Kattokorvauksen määrä lääkkeen kohdalla';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);

	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
	RETAIN YLI_KATTO_CUMU;
	/* Katsotaan, onko ylittänyt katon */
	IF ROMAV_KERTY_SIMUL >= &LaakeKatto THEN YLI_KATTO=1;ELSE YLI_KATTO=0;
	
	IF FIRST.hnro THEN yli_katto_cumu=yli_katto;
		ELSE yli_katto_cumu=yli_katto_cumu + yli_katto;
	IF yli_katto_cumu=0 THEN &tulos=ROMAV_SIMUL;
	/* Ensimmäinen katon ylittänyt lasketaan hieman eri tavalla, koska se on osittain lääkekaton alla */
	IF ANJA="" AND yli_katto_cumu=1 AND ROMAV_KERTY_SIMUL - &LaakeKatto >= &LaakekattoYlitKiintea THEN 
		&tulos = ROMAV_SIMUL - (ROMAV_KERTY_SIMUL - &LaakeKatto) + &LaakekattoYlitKiintea;
	IF ANJA="" AND yli_katto_cumu=1 AND ROMAV_KERTY_SIMUL - &LaakeKatto < &LaakekattoYlitKiintea THEN 
		&tulos = ROMAV_SIMUL;
	
	IF ANJA="" AND yli_katto_cumu > 1 AND ROMAV_SIMUL >= &LaakekattoYlitKiintea THEN 
		&tulos = &LaakekattoYlitKiintea;
	IF ANJA="" AND yli_katto_cumu > 1 AND ROMAV_SIMUL < &LaakekattoYlitKiintea THEN 
		&tulos = ROMAV_SIMUL;
	/* Annosjakeluissa on oma laskentakaavansa, koska omavastuu on viikoittainen */
	IF ANJA NE "" AND yli_katto_cumu=1 AND ROMAV_KERTY_SIMUL - &LaakeKatto >= 2*&LaakekattoYlitKiinteaPoikk THEN 
		&tulos = ROMAV_SIMUL - (ROMAV_KERTY_SIMUL - &LaakeKatto) + 2*&LaakekattoYlitKiinteaPoikk;
	IF ANJA NE "" AND yli_katto_cumu=1 AND ROMAV_KERTY_SIMUL - &LaakeKatto < 2*&LaakekattoYlitKiinteaPoikk THEN 
		&tulos = ROMAV_SIMUL;
	
	IF ANJA NE "" AND yli_katto_cumu > 1 AND ROMAV_SIMUL >= 2*&LaakekattoYlitKiinteaPoikk THEN 
		&tulos = 2*&LaakekattoYlitKiinteaPoikk;
	IF ANJA NE "" AND yli_katto_cumu > 1 AND ROMAV_SIMUL < 2*&LaakekattoYlitKiinteaPoikk THEN 
		&tulos = ROMAV_SIMUL;
	/* Jos on lisäpakkaus ja ylittää katon niin silloin ei lasketa omavastuuta. Omavastuu sisältyy "emopakkauksen" riviin */
	IF JTJNO>0 AND yli_katto_cumu > 1 THEN
		&tulos = 0;
	

/*

*/
%MEND LaakKattoKorvausMaksettava;

/* 7. Makro, joka laskee lääkkeen vähittäismyyntihinnan */
/* Hahmottelu kesken!!! Ei välttämättä vielä toimi luotettavasti */
*Makron parametrit:
	tulos: Makron tulosmuuttuja (luultavasti tekee monta tulosmuuttujaa nykyisellään)
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään
	;

%MACRO LaakTaksaVMH(tulos, mvuosi, mkuuk, minf)/
DES = 'LAAKE: Lääkkeen vähittäismyyntihinta';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);

	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
	* Korjaan tukkuhinnat, laitan vanhan muuttujan päälle;
	IF RSTATUS^="1" THEN THINTA=&TukkuHintaAle*THINTA;
	* Ennen vuotta 2013 eri taksat, ne pitää ehkä myös laittaa tähän;
	* Ei tarvita enää;
	*IF &mvuosi>2013 THEN;
		/* Reseptilääkkeet */
		IF RSTATUS="1" THEN DO;
			 IF THINTA >= &TaksaEuroRes1 and THINTA <= &TaksaEuroRes2 THEN &tulos = &TaksaKerroinRes1 * THINTA + &TaksaKerroinRes2;
			 IF THINTA > &TaksaEuroRes2 and THINTA <= &TaksaEuroRes3 THEN &tulos = &TaksaKerroinRes3 * THINTA + &TaksaKerroinRes4;
			 IF THINTA > &TaksaEuroRes3 and THINTA <= &TaksaEuroRes4 THEN &tulos = &TaksaKerroinRes5 * THINTA + &TaksaKerroinRes6;
			 IF THINTA > &TaksaEuroRes4 and THINTA <= &TaksaEuroRes5 THEN &tulos = &TaksaKerroinRes7 * THINTA + &TaksaKerroinRes8;
			 IF THINTA > &TaksaEuroRes5 THEN &tulos = &TaksaKerroinRes9 * THINTA + &TaksaKerroinRes10;
		END;
		ELSE
		/* Muut lääkkeet */
		IF	RSTATUS ^= "1" THEN DO;
			IF THINTA >= &TaksaEuroEiRes1 AND THINTA <= &TaksaEuroEiRes2 THEN &tulos = &TaksaKerroinEiRes1 * THINTA + &TaksaKerroinEiRes2;
			IF THINTA > &TaksaEuroEiRes2 AND THINTA <= &TaksaEuroEiRes3 THEN &tulos = &TaksaKerroinEiRes3 * THINTA + &TaksaKerroinEiRes4;
			IF THINTA > &TaksaEuroEiRes3 AND THINTA <= &TaksaEuroEiRes4 THEN &tulos = &TaksaKerroinEiRes5 * THINTA + &TaksaKerroinEiRes6;
			IF THINTA > &TaksaEuroEiRes4 AND THINTA <= &TaksaEuroEiRes5 THEN &tulos = &TaksaKerroinEiRes7 * THINTA + &TaksaKerroinEiRes8;
			IF THINTA > &TaksaEuroEiRes5 THEN &tulos = &TaksaKerroinEiRes9 * THINTA + &TaksaKerroinEiRes10;
		END;
	*Vero lisää;
	&tulos=round(&tulos + &LaakeAlv*&tulos, 0.01);
%MEND LaakTaksaVMH;


/* 8. Makro, joka laskee lääkkeen viitehinnan */
/* Hahmottelu kesken!!! */
/* Luultavasti viitehinnan laskenta tehdään keskellä makroa LaakTaksa */
/* Viitehinta pitää laskea uudestaan, jos tehdään muutoksia taksataulukoihin tai tukkuhintoihin */
/* Viitehinta on vuosikvartaalin ensimmäisen 2 viikon jakson halvin hinta + 0,5e substituutioryhmässä */
*Makron parametrit:
	tulos: Makron tulosmuuttuja, alkuomavastuun määrä
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään

	;

%MACRO LaakViiteHinta(tulos, mvuosi, mkuuk, minf)/
DES = 'LAAKE: Lääkkeen myyntihinta';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);

	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
	if first.SUBKOODI
		then do;
			if VMH_SIMU<&LaakeTaksaPutkiRaja then &tulos=VMH_SIMU + &LaakeTaksaPutki1; 
			if VMH_SIMU>=&LaakeTaksaPutkiRaja then  &tulos=VMH_SIMU + &LaakeTaksaPutki2; 
			output;
			end;

%MEND LaakViiteHinta;

/* 9. Makro, joka laskee lääkkeen lopullisen myyntihinnan */
/* Hahmottelu kesken!!! Ei välttämättä vielä toimi luotettavasti */
*Makron parametrit:
	tulos: Makron tulosmuuttuja (luultavasti tekee monta tulosmuuttujaa nykyisellään)
	mvuosi: Vuosi, jonka lainsäädäntöä käytetään
	;

%MACRO LaakTaksa(tulos, mvuosi, mkuuk, minf)/
DES = 'LAAKE: Lääkkeen myyntihinta';
	%HaeParam&TYYPPI(&mvuosi, &mkuuk, &LAAKE_PARAM, PARAM.&PLAAKE);
	%ParamInf&TYYPPI(&mvuosi, &mkuuk, &LAAKE_MUUNNOS, &minf);

	%LuoKuuID(kuuid, &mvuosi, &mkuuk);
	/* Jos lasketaan viitehinta, pitää viitehintamakron tulla tähän */
	 *Lasketaan kokonaiskustannus pakkauksen hinnan perusteella toimitusmaksuineen;
	/* Laitan tähän varmuuden vuoksi alkuperäisen kustannuksen */
	/* Mikäli mitään hintaa ei löydy, pidetään vanha hinta */
	KUST_SIMUL=KUST;
	RVMK_SIMU=RVMK;
	IF VMH_SIMU>=0 THEN KUST_SIMUL = PLKM*VMH_SIMU + &LaakeToimMaksu + &LaakeAlv * &LaakeToimMaksu;

	IF VIHINTA_SIMU>0 THEN 
			RVMK_SIMU=PLKM*VIHINTA_SIMU + &LaakeToimMaksu + &LaakeAlv*&LaakeToimMaksu;
		ELSE RVMK_SIMU=KUST_SIMUL;
	*annosjakeluja varten pitää tehdä ylimääräinen muuttuja, jonka laskemisessa käytetään alkuperäistä kustannusta;
	*käytetään toistaiseksi olettamuksena 2 viikon toimitusta, koska toimitusväliin ei ole luotettavaa muuttujaa;
	IF ANJA^="" THEN PRO_ANJA = KUST / (PLKM*VHINTA + round((&LaakeToimMaksu + &LaakeAlv*&LaakeToimMaksu)/6, 0.01));
	IF VIHINTA_SIMU>0 AND ANJA^="" AND PRO_ANJA^=. THEN RVMK_SIMU = PRO_ANJA * VIHINTA_SIMU + round((&LaakeToimMaksu + &LaakeAlv*&LaakeToimMaksu)/6, 0.01);
	IF (VIHINTA_SIMU=0 OR VIHINTA_SIMU=.) AND ANJA^="" AND PRO_ANJA^=. THEN RVMK_SIMU = PRO_ANJA * VMH_SIMU + round((&LaakeToimMaksu + &LaakeAlv*&LaakeToimMaksu)/6, 0.01);
	IF VMH_SIMU^=. AND ANJA^="" AND PRO_ANJA^=. THEN KUST_SIMUL = PRO_ANJA * VMH_SIMU + round((&LaakeToimMaksu + &LaakeAlv*&LaakeToimMaksu)/6, 0.01);
    
	IF VMH_SIMU^=. AND ANJA^="" AND PRO_ANJA^=. AND KUST_SIMUL > RVMK_SIMU  THEN RVMK_SIMU = KUST_SIMUL;
	/* Lisäpakkaus, ei toimituskuluja */
	IF VMH_SIMU^=. AND JTJNO>0 THEN KUST_SIMUL = PLKM * VMH_SIMU;
	IF VIHINTA_SIMU>0 AND JTJNO>0 THEN RVMK_SIMU = PLKM*VIHINTA_SIMU;
	IF (VIHINTA_SIMU=0 OR VIHINTA_SIMU=.) AND JTJNO>0 AND VMH_SIMU^=. THEN RVMK_SIMU = PLKM * VMH_SIMU;
	*Lääkärin kielto vaihdolle, ei lisäomavastuuta joten RVMK=KUST;
	IF RGENK in ('L', 'P', 'E', 'S', 'V') THEN RVMK_SIMU = KUST_SIMUL;
	*RVMK ei voi olla suurempi kuin KUST;
	IF RVMK_SIMU > KUST_SIMUL THEN RVMK_SIMU = KUST_SIMUL;
	RVMK_SIMU=round(RVMK_SIMU, 0.01);
	KUST_SIMUL=round(KUST_SIMUL, 0.01);
	* Jos kyseessä on lääkeseos, ei hintaa muuteta. Tätä pitää ehkä muuttaa jatkossa...; 
	IF RTUN="L" THEN DO;
			KUST_SIMUL=KUST;
			RVMK_SIMU=RVMK;
		END;
	

%MEND LaakTaksa;

